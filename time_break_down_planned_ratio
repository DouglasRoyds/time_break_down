#!/usr/bin/gawk -f
# Compute planned time ratio as a percentage.
# Excludes leave.
# Usage:
#
#     cat archive/2012/* | time_break_down_task_times | time_break_down_planned_ratio
#
#     for f in archive/2012/*; do
#        cat $f | time_break_down_task_times | time_break_down_planned_ratio
#     done | sort -n

BEGIN {
   FS = "\t"
   OFS = "\t"
   TIME = 1
   SUBTASK = 2
   TASK = 3
   PROJECT = 4
   DATE = 5

   TRUE = 1
   FALSE = 0

   error_number = 0

   unplanned_tasks = "assist|spec|support|admin|mail|planning|computer|supervision|training|reading|procs|stuff|vim|jenkins"
}

($TASK ~ "^$|leave") {
   next
}

($PROJECT ~ "non-project") {
   total += $TIME
   next
}

($TASK ~ unplanned_tasks) {
   total += $TIME
   next
}

# Everything else is planned project time
{
   total += $TIME
   planned += $TIME
   next
}

END {
   if (total > 0) {
      ratio = int(100 * planned / total + 0.5)
      print ratio
   }
}

